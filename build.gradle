buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.6'
    }
}

plugins {
  id 'application'
  id 'org.openjfx.javafxplugin' version '0.0.7'
}

apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'
apply from: 'deploy.gradle'

version=artifactVersion
group=artifactGroupId
sourceCompatibility = '1.8'
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

if (!hasProperty('mainClass')) {
    ext.mainClass = 'de.jensd.extensions.demo.App'
}

repositories {
    mavenCentral()
    mavenLocal()
    jcenter()
    maven {
        url 'https://s3.amazonaws.com/cirrus-link-maven-repo'
    }
}

javafx {
    version = "12.0.1"
    modules = [ 'javafx.base' ]
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
    classifier "sources"
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    pkg {
        repo = "maven"
        name = artifactId
        userOrg = artifactUserOrg
        publications = artifactPublications
        issueTrackerUrl = artifactIssueTrackerUrl
        websiteUrl = artifactSiteUrl
        vcsUrl = artifactGitUrl
        licenses = artifactLicenses
        publicDownloadNumbers = true
        publish = true
        version {
            name = artifactVersion
            desc =  artifactDesc
            vcsTag = artifactVcsTag
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourceJar {
                classifier "sources"
            }

            artifact javadocJar {
                classifier "javadoc"
            }
        }
    }
}
dependencies {
    compile 'de.jensd:addon-commons:1.1.0'
    compile 'commons-codec:commons-codec:1.10'
    compile 'org.codehaus.jackson:jackson-mapper-asl:1.9.13'
    compile 'org.apache.logging.log4j:log4j-api:2.6'
    compile 'org.apache.logging.log4j:log4j-core:2.6'
    compile 'org.apache.logging.log4j:log4j-slf4j-impl:2.6'
    compile 'com.fasterxml.jackson.core:jackson-core:2.9.4'
    compile 'com.cirruslink.sparkplug:sparkplug-java:1.1.11'
    compile 'org.msgpack:msgpack-core:0.8.17'
    testCompile 'junit:junit:4.10'
}

wrapper {
    gradleVersion = '4.6'
}
